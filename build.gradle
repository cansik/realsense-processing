plugins {
    id 'java-library'
    id 'org.bytedeco.gradle-javacpp-platform' version "$javacppVersion"
}

group 'ch.bildspur'
version '2.5.1'

sourceCompatibility = 1.8

def rsVersion = 'v2.53.1-0.6.0'


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    jar.archiveName = outputName + '.jar'
}

javadoc {
    source = sourceSets.main.allJava
}

dependencies {
    // release
    implementation "com.github.cansik:librealsense-java:$rsVersion"

    // todo: check why transitive dependencies currently do not work!
    // java cpp
    implementation group: 'org.bytedeco', name: 'javacpp', version: "$javacppVersion"

    // librealsense2
    implementation group: 'org.bytedeco', name: 'librealsense2', version: '2.53.1-1.5.9'
    implementation group: 'org.bytedeco', name: 'librealsense2-platform', version: '2.53.1-1.5.9'

    // compile
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'

    // processing
    implementation fileTree(include: ["core.jar", "jogl-all-main.jar", "gluegen-rt-main.jar"], dir: 'core-libs')
}

task fatJar(type: Jar) {
    archiveFileName = "$outputName-complete.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.runtimeClasspath
    from {
        (configurations.runtimeClasspath).filter( {! (it.name =~ /core.jar/ ||
                it.name =~ /jogl-all-main.jar/ ||
                it.name =~ /gluegen-rt-main.jar/)}).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

// add processing library support
apply from: "processing-library.gradle"